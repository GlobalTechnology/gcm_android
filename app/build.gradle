buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "com.newrelic.agent.android:agent-gradle-plugin:4.244.0"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'newrelic'

group 'org.ccci.gto.android'
version '0.8.5-SNAPSHOT'

def generateVersionCode(version) {
    def code = 0;
    version.replaceAll('[^0-9\\.]','').split('\\.').each{
        code = (1000 * code) + Integer.valueOf(it);
    }
    return code;
}

android {
    compileSdkVersion 21
    buildToolsVersion '19.1.0'

    defaultConfig {
        applicationId "com.expidevapps.android.measurements"

        versionName project.version
        versionCode generateVersionCode(project.version)

        minSdkVersion 10
        targetSdkVersion 21

        buildConfigField "String", "GCM_BASE_URI", "\"https://measurements.global-registry.org/v1/\""
        buildConfigField "String", "NEW_RELIC_API_KEY", "\"AA3128e4005a5f2d68e0d232aebec6e9936950e042\""
        buildConfigField "long", "THEKEY_CLIENTID", "7439283632057308L"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile file(androidKeystorePath)
            storePassword androidKeystoreStorePassword
            keyAlias androidKeystoreKeyAlias
            keyPassword androidKeystoreKeyPassword
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            buildConfigField "String", "GCM_BASE_URI", "\"https://stage-measurements.global-registry.org/v1/\""
            buildConfigField "String", "NEW_RELIC_API_KEY", "\"AAfd80ddf74e333f27c92d3f89f125be5cecf9c532\""

            resValue "string", "google_maps_api_key", "AIzaSyCsM4eK9ZND1yZXv9Lg004peeepnKGH0EY"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            resValue "string", "google_maps_api_key", "AIzaSyAATaK4l6LRKP5KA61KI2NkHAXd4ilS7NM"
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name "maven.gcx.org"
        url "https://maven.gcx.org/"
    }
    maven {
        name "achartengine"
        url "https://repository-achartengine.forge.cloudbees.com/snapshot/"
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.google.android.gms:play-services-maps:6.5.87'
    compile 'com.google.maps.android:android-maps-utils:0.3.4'

    compile 'com.newrelic.agent.android:android-agent:4.244.0'

    compile 'com.jakewharton:butterknife:6.1.0'

    compile 'me.thekey.android:thekey-api:0.7.1-SNAPSHOT'
    compile 'me.thekey.android:thekey-lib:0.7.1-SNAPSHOT'

    compile 'org.ccci.gto.android:gto-support:0.4.3-SNAPSHOT'

    compile 'com.github.machinarius:preferencefragment:0.1.1'
    compile 'org.achartengine:achartengine:1.2.0'
}

apply from: 'maven_publish.gradle'
afterEvaluate { project ->
    artifacts {
        archives(file("build/outputs/mapping/release/mapping.txt")) {
            classifier 'mapping'
            type 'txt'
            builtBy proguardRelease
        }
        archives artifactSourcesJar
    }
}
