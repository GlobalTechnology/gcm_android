buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath 'com.newrelic.agent.android:agent-gradle-plugin:5.1.2'
        classpath 'io.fabric.tools:gradle:1.19.2'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'newrelic'

group 'org.ccci.gto.android'
version '0.8.11-SNAPSHOT'

def generateVersionCode(version) {
    def code = 0;
    version.replaceAll('[^0-9\\.]','').split('\\.').each{
        code = (1000 * code) + Integer.valueOf(it);
    }
    return code;
}

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        applicationId "com.expidevapps.android.measurements"

        versionName project.version
        versionCode generateVersionCode(project.version)

        minSdkVersion 10
        targetSdkVersion 22

        buildConfigField "long", "THEKEY_CLIENTID", "7439283632057308L"
        buildConfigField "int", "GMA_API_VERSION", "4"
        buildConfigField "String", "GOOGLE_ANALYTICS_CLIENT_ID", "\"UA-29919940-7\""

        resValue "string", "google_maps_api_key", "AIzaSyAATaK4l6LRKP5KA61KI2NkHAXd4ilS7NM"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    signingConfigs {
        demo {
            storeFile file(androidKeystorePath)
            storePassword androidKeystoreStorePassword
            keyAlias androidKeystoreDemoKeyAlias
            keyPassword androidKeystoreKeyPassword
        }
        release {
            storeFile file(androidKeystorePath)
            storePassword androidKeystoreStorePassword
            keyAlias androidKeystoreKeyAlias
            keyPassword androidKeystoreKeyPassword
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            buildConfigField "String", "GMA_API_BASE_URI", "\"https://stage-measurements.global-registry.org/\""
            buildConfigField "String", "NEW_RELIC_API_KEY", "\"AAfd80ddf74e333f27c92d3f89f125be5cecf9c532\""

            resValue "string", "google_maps_api_key", "AIzaSyCsM4eK9ZND1yZXv9Lg004peeepnKGH0EY"
        }
        demo {
            applicationIdSuffix ".demo"
            versionNameSuffix "-demo"

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if(signingConfigs.demo.storeFile.exists()) {
                signingConfig signingConfigs.demo
            }

            buildConfigField "String", "GMA_API_BASE_URI", "\"https://stage-measurements.global-registry.org/\""
            buildConfigField "String", "NEW_RELIC_API_KEY", "\"AA8cb9761bdbec4e9ada73f4f4953f5c347f315d37\""
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if(signingConfigs.release.storeFile.exists()) {
                signingConfig signingConfigs.release
            }

            buildConfigField "String", "GMA_API_BASE_URI", "\"https://measurements.global-registry.org/\""
            buildConfigField "String", "NEW_RELIC_API_KEY", "\"AA3128e4005a5f2d68e0d232aebec6e9936950e042\""
        }
    }

    applicationVariants.all {
        buildConfigField "String", "ACCOUNT_TYPE", '"' + applicationId + '.account"'
        resValue "string", "account_type", applicationId + ".account"
        buildConfigField "String", "SYNC_AUTHORITY", '"' + applicationId + '.authority"'
        resValue "string", "sync_authority", applicationId + ".authority"
    }
}

repositories {
    mavenLocal()
    jcenter()
    maven {
        name "maven.gcx.org"
        url "https://maven.gcx.org/"
    }
    // This is a temporary workaround until ViewPagerIndicator is distributed as an aar
    maven {
        url 'https://github.com/Dahlgren/vpi-aar/raw/master/'
    }
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.google.android.gms:play-services-analytics:7.5.0'
    compile 'com.google.android.gms:play-services-maps:7.5.0'
    compile 'com.google.guava:guava:18.0'
    compile 'com.google.maps.android:android-maps-utils:0.3.4'

    compile 'com.crashlytics.sdk.android:crashlytics:2.4.0'
    compile 'com.newrelic.agent.android:android-agent:5.1.2'

    compile 'com.jakewharton:butterknife:6.1.0'

    compile 'me.thekey.android:thekey-api:1.0.0'
    compile 'me.thekey.android:thekey-lib:1.0.0'

    compile 'org.ccci.gto.android:gto-support-api:0.6.2'
    compile 'org.ccci.gto.android:gto-support-core:0.6.2'
    compile 'org.ccci.gto.android:gto-support-db:0.6.2'
    compile 'org.ccci.gto.android:gto-support-recyclerview:0.6.2'

    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'org.slf4j:slf4j-android:1.7.12'

    compile 'com.github.machinarius:preferencefragment:0.1.1'
    compile 'com.github.PhilJay:MPAndroidChart:v2.1.0'
    compile 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.6.2'
    compile 'com.viewpagerindicator:library:2.4.2-SNAPSHOT'
    compile 'net.danlew:android.joda:2.7.2'

    debugCompile 'com.facebook.stetho:stetho:1.1.1'
}

apply from: 'maven_publish.gradle'
afterEvaluate { project ->
    artifacts {
        archives(file("build/outputs/mapping/release/mapping.txt")) {
            classifier 'mapping'
            type 'txt'
            builtBy proguardRelease
        }
        archives(file("build/outputs/apk/gma-demo.apk")) {
            classifier 'demo'
            type 'apk'
            builtBy assembleDemo
        }
        archives(file("build/outputs/mapping/demo/mapping.txt")) {
            classifier 'demo-mapping'
            type 'txt'
            builtBy proguardDemo
        }
        archives artifactSourcesJar
    }
}
